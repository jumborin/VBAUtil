Option VBASupport 1
' 上記はLibreOfficeでの開発用のため、MicosoftOfficeで実施する場合、削除すること。

Option Explicit

Private シート As Worksheet
Const シート名 = "コメント一覧"

Public Sub シート設定()
    ThisWorkbook.Worksheets.Add(After:=Worksheets(Worksheets.Count)).Name = シート名
    Set シート = ThisWorkbook.Worksheets(シート名)
    Call タイトル行出力
End Sub

Private Sub タイトル行出力()
    With シート
        .Cells(1, 1).Value = "No"
        .Cells(1, 2).Value = "ファイル名"
        .Cells(1, 3).Value = "ページ"
        .Cells(1, 4).Value = "行"
        .Cells(1, 5).Value = "作成者"
        .Cells(1, 6).Value = "コメント内容"
    End With
    Columns("A:Z").EntireColumn.AutoFit
End Sub

Public Sub データ出力(ByVal ワードファイル As Word.Document)
    Dim 行 As Long
    Dim コメント番号 As Long
    
    On Error Resume Next
    
    行 = シート.Cells(1048576, 1).End(xlUp).Row + 1
    For コメント番号 = 1 To ワードファイル.Comments.Count
        With シート
            .Cells(行, 1).Value = "=ROW()-1"
            .Cells(行, 2).Value = ワードファイル.Name
            .Cells(行, 3).Value = ワードファイル.Comments(コメント番号).Scope.Information(wdActiveEndAdjustedPageNumber)
            .Cells(行, 4).Value = ワードファイル.Comments(コメント番号).Scope.Information(wdFirstCharacterLineNumber)
            .Cells(行, 5).Value = ワードファイル.Comments(コメント番号).Author
            .Cells(行, 6).Value = ワードファイル.Comments(コメント番号).Range
        End With
        行 = 行 + 1
    Next コメント番号
    Columns("A:Z").EntireColumn.AutoFit
End Sub
